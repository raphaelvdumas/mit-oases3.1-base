      SUBROUTINE PLOTNY(XB,YB,ICS,ITYPE)
C     PLOTS A LINE FROM PRESENT POINT TO POINT XB,YB (IN INCHES)
C     ICS=3 PEN UP
C        =2 PEN DOWN
C     ITYPE=0 OR 1 ORDINARY LINE
C          =2 HEAVY LINE
C          =3 DOTTED LINE
C     MUST BE INITIALIZED BY CALL TO PLOTNY WITH PEN UP.
C     OCEANOGRAPHY/EMR   DECEMBER 1969.
C
      IF(ICS-3)5,10,5
    5 IF(ITYPE-2)10,20,30
C
C     ORDINARY LINE   ITYPE=0 OR 1.
C
   10 CALL PLOT(XB,YB,ICS)
      GO TO 100
C
C     HEAVY LINE   ITYPE=2
C
   20 DX=XB-XA
      DY=YB-YA
      DS=SQRT(DX*DX+DY*DY)+.001
      DXD=DX*.01/DS
      DYD=DY*.01/DS
      CALL PLOT(XB,YB,2)
      CALL PLOT(XB+DYD,YB-DXD,2)
      CALL PLOT(XA+DYD,YA-DXD,2)
      CALL PLOT(XA-DYD,YA+DXD,2)
      CALL PLOT(XB-DYD,YB+DXD,2)
      CALL PLOT(XB,YB,2)
      GO TO 100
C
C     DOTTED LINE   ITYPE=3
C
   30 SX=XB-XA
      SY=YB-YA
      S=SQRT(SX*SX+SY*SY)+.001
      NSEG=(S+.05)*10.
      NSEG=MAX0(NSEG,1)
      DX=SX/NSEG
      DXQ=DX*.25
      DX3Q=DX*.75
      DY=SY/NSEG
      DYQ=DY*.25
      DY3Q=DY*.75
      DO 35 K=1,NSEG
      XK=XA+DX*(K-1)
      YK=YA+DY*(K-1)
      CALL PLOT(XK+DXQ,YK+DYQ,2)
      CALL PLOT(XK+DX3Q,YK+DY3Q,3)
   35 CALL PLOT(XK+DX,YK+DY,2)
C
C     SAVE POINT A FOR NEXT TIME
C
  100 XA=XB
      YA=YB
      RETURN
      END
